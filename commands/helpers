#!/bin/zsh

# import helpers
source `dirname $0`/../shell/functions

# OS Detection
OS_TYPE="$(uname -s)"
if [[ "$OS_TYPE" == "Darwin" ]]; then
  IS_MACOS=true
  IS_LINUX=false
else
  IS_MACOS=false
  IS_LINUX=true
fi

# Minimal mode for dev containers
IS_MINIMAL=${DOTFILES_MINIMAL:-false}

DOTFILES=~/.dotfiles
DOTFILES_SHELL=$DOTFILES/shell
DOTFILES_CONFIGS=$DOTFILES/configs
DOTFILES_PACKAGES=$DOTFILES/pkgs

# Directory / File specific
BREW_BUNDLE_FILE=$DOTFILES_PACKAGES/Brewfile
NPM_PACKAGES=$DOTFILES_PACKAGES/npm

# Platform-specific path functions
function get_vscode_config_dir {
  if $IS_MACOS; then
    echo "$HOME/Library/Application Support/Code/User"
  else
    echo "$HOME/.config/Code/User"
  fi
}

function get_pnpm_home {
  if $IS_MACOS; then
    echo "$HOME/Library/pnpm"
  else
    echo "$HOME/.local/share/pnpm"
  fi
}

function get_1password_agent_sock {
  if $IS_MACOS; then
    echo "$HOME/Library/Group Containers/2BUA8C4S2C.com.1password/t/agent.sock"
  else
    echo "$HOME/.1password/agent.sock"
  fi
}

# Logging
WARN='\033[0;31m'
FG='\033[1;34m'
NC='\033[0m'

# Logging functions
function ewrn { echo "${FG}[${WARN}!${FG}] ${1}${NC}"; }
function eerr { echo "${FG}[${WARN}x${FG}] ${1}${NC}"; }
function einf { echo "${FG}[i] ${1}${NC}"; }
function eask { printf "${FG}[?] ${1} (y/n)${NC} "; }
function link {
  # Remove existing file/symlink/directory if it exists
  [[ -e "$2" || -L "$2" ]] && rm -rf "$2"
  ln -s "$1" "$2"
}

# Ask then take action function
function askdo {
  while true; do
    eask "$2"
    read -r choice
    case "$choice" in
      y|Y|yes|YES ) $1; return;;
      n|N|no|NO ) return;;
      * ) ewrn "Please answer y or n";;
    esac
  done
}

# Ask sudo once
function sudokeepalive {
	# Ask for mac password
	sudo -v
	# Keep-alive
	while true; do sudo -n true; sleep 60; kill -0 "$$" || exit; done 2>/dev/null &
}

function create_dir_if_not_exists {
  if [ ! -d "$1" ]; then
    mkdir -p "$1"
  fi
}

function backup_file {
  if [ -f "$1" ]; then
    mv "$1" "$1.bak"
    einf "File $1 backed up to $1.bak"
  fi
}

function backup_and_link_file {
  backup_file "$2"
  link "$1" "$2"
}

function is_m1_mac {
  if [[ "$(arch)" == 'arm64' ]]; then
    return 0
  else 
    return 1
  fi
}

function reload_shell {
  einf "Reloading shell profile... "
  source "$HOME/.zshrc"
}

function ensure_brew {
  if ! $IS_MACOS; then
    return 1
  fi

  if type "brew" > /dev/null 2>&1; then
    return 0
  fi

  # Check if brew exists at ARM Mac location but not in PATH
  if [[ -x /opt/homebrew/bin/brew ]]; then
    eval "$(/opt/homebrew/bin/brew shellenv zsh)"
    return 0
  fi

  einf "Homebrew not found, installing..."
  /bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"
  eval "$(/opt/homebrew/bin/brew shellenv zsh)"
}

function ensure_brew_package {
  if ! $IS_MACOS; then
    return 1
  fi

  ensure_brew

  if brew list "$1" &> /dev/null; then
    return 0
  fi

  einf "Installing $1 via Homebrew..."
  brew install "$1"
}
